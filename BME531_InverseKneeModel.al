% INVERSE DYNAMICS KNEE MODEL
% USE THIS AS SAMPLE TEMPLATE TO BUILD YOUR FINAL MODEL
% MODELING LEFT KNEE
% +1: ANTERIOR, +2: SUPERIOR, +3: MEDIAL

% FOOT, FEMUR, TIBIA, PATELLA, AND PELVIS.
% HINGE JOINTS FOR THE ANKLE, BALL AND SOCKET FOR HIP. THERE IS ONE POINT OF CONTACT FOR EACH OF THESE JOINTS
% THE KNEE IS MODELED IN GENERIC FASHION AND THERE ARE TWO CONTACT POINTS.
% PATELLOFEMORAL IS MODELED IN GENERIC FASHION. THERE IS JUST ONE CONTACT POINT.

% THE LCL, THE PATELLAR LIGAMENT AND ONE OF THE VASTUS FIBERS ARE THE THREE SOFT TISSUES CURRENTLY PRESENT.
% THE LCL IS TREATED AS A SPECIFIED KNOWN FORCE. THE FORCE IS CALCULATED BASED ON LIGAMENT STIFFNESS PROPERTIES.
% THE PATELLA LIGAMENT/TENDON FORCE IS TREATED AS AN UNKNOWN THAT IS BEING CALCULATED FROM THE MODEL.
% THE VASTUS MUSCLE FIBER FORCE IS ALSO SOLVED FOR (UNKNOWN IN THE SYSTEM). USE IT AS AN EXAMPLE FOR WRAPPING DEFINITION (IN THE 2ND PART. FOR THE 1ST PART YOU DO NOT NEED TO DEFINE WRAPPING).

% YOU NEED TO ADD ALL OTHER RELEVANT LIGAMENTS AND MUSCLE FIBERS AS DEFINED IN THE PROBLEM

AUTOZ ON
% DEFINING FRAMES AND BODIES
NEWTONIAN N
BODIES FEM,TIB,PAT,FOOT,PEL % ASSUMPTION: IMPLANT COMPONENTS ARE RIGIDLY FIXED TO THEIR CORRESPONDING BONES

% CONSTANTS TO CONVERT DEGREES TO RADIANS AND RADIANS TO DEGREES
CONDTOR=3.1415927/180
CONRTOD=180/3.1415927

% DEFINING INERTIA CONSTANTS
CONSTANTS MASSFEM, MASSTIB, MASSPAT, MASSFOOT, MASSPEL, G, BW   % EG: MASSFEM=0.11*BW/G
CONSTANTS IFEM{3},ITIB{3},IPAT{3},IFOOT{3},IPEL{3}

MASS FEM=MASSFEM
MASS TIB=MASSTIB
MASS PAT=MASSPAT
MASS FOOT=MASSFOOT
MASS PEL=MASSPEL

INERTIA FEM,IFEM1,IFEM2,IFEM3
INERTIA TIB,ITIB1,ITIB2,ITIB3
INERTIA PAT,IPAT1,IPAT2,IPAT3
INERTIA FOOT,IFOOT1,IFOOT2,IFOOT3
INERTIA PEL,IPEL1,IPEL2,IPEL3

GRAVITY(-G*N2>)
INPUT G=9.81

% DEFINING JOINT RELATED POINTS
POINTS FOOTNEWTONIAN % POINT BETWEEN THE FOOT AND THE NEWTONIAN FRAME
POINTS ANKLEFOOT,ANKLETIB % POINTS FOR ANKLE JOINT
POINTS KNEETIBMED,KNEETIBLAT,KNEEFEMURMED,KNEEFEMURLAT % POINTS FOR KNEE (2 SET OF CONTACT POINTS)
POINTS HIPFEM,HIPPEL,PELNEWTONIAN % POINTS FOR HIP
POINTS PATFEM,FEMPAT % POINTS FOR PATELLOFEMORAL JOINT

% DEFINING REFERENCE POINTS
POINTS TIBREF,FEMREF

% DEFINING GRF, PATLIG, AND MUSCLE POINTS
POINTS GRF % FOR GROUND REACTION FORCE ON FOOT
POINTS TIBPATLIG, PATPATLIG % ATTACHMENT COORDS FOR PATLIG

% INTRODUCING CONSTANTS AND VARIABLES TO DEFINED DISTANCES AND OTHER PARAMETERS
CONSTANTS FOOTNEWT2FOOTO{3},FOOTO2ANKLE{3} % DISTANCES ON FOOT
CONSTANTS ANKLE2TIBREF{3},TIBREF2TIBO{3} % DISTANCES ON TIBIA
CONSTANTS TIBREF2PATLIG{3},PATO2PATLIG{3} % ATTACHMENT OF PATLIG WRT TO BONE REFERENCE POINTS
CONSTANTS FEMREF2FEMO{3},FEMREF2HIP{3} % DISTANCES ON FEMUR
CONSTANTS HIPPEL2PELO{3},PELO2PELNEWT{3} % DISTANCES ON PELVIS

% DEFINING THE AUXILIARY GENERALIZED SPEEDS
VARIABLES U{30}' % 5 BODIES X 6 PER BODY

% INTRODUCING SPECIFIED INPUTS
SPECIFIED GRFFORCE{3} % THIS IS KNOWN FROM FORCE PLATE. FUNCTION OF T AS OBTAINED FROM CURVE FITS. DO NOT FORGET TO INPUT VALUES OF THE SPECIFIED
SPECIFIED THETAFOOT{3}'', THETATIB'',THETAFEM{3}'',THETAPAT{3}'',THETAPEL{3}'' % KNOWN ANGLES OF THE BONES
SPECIFIED TIBREF2FEMREF{3}'' % DISTANCE FROM REFERENCE POINT ON TIBIA TO FEMUR WHO POSTION/VELOCITY/ACCELERATION IS KNOWN. USED TO REALITICALLY MODEL THE KNEE JOINT
SPECIFIED FEMREF2KNEEFEMURMED{3}'', FEMREF2KNEEFEMURLAT{3}'' % KNOWN DISTANCES THE TWO KNEE CONTACT POINTS FROM THE FEMUR REFERENCE
SPECIFIED FEMREF2PATO{3}'', FEMREF2FEMPAT{3}'' % DISTANCES TO MODEL THE PATELLOFEMORAL JOINT

% INTRODUCING FORCE AND TORQUE VARIABLES
VARIABLES ANKLEFORCE{3},KNEEFORCEMED{3},KNEEFORCELAT{3},HIPFORCE{3},PATFEMFORCE{3},SPINEFORCE{3} % CONTACT FORCES
VARIABLES FOOTTOR{3},FEMTOR{3},TIBTOR{3},PATTOR{3},PELTOR{3} % BODY TORQUES
VARIABLES PATLIGFORCE % PATLIG FORCE THAT IS TREATED AS AN UNKNOWN

%%%%% \/ %%%%%
% INPUT KNEE LIGAMENT INFORMATION HERE
%CONSTANTS REFSTRAIN

% LCL
%POINTS TIBLCL, FEMLCL % ATTACHMENT COORDS FOR LCL
%CONSTANTS TIBREF2LCL{3},FEMREF2LCL{3} % ATTACHMENT OF LCL WRT TO BONE REFERENCE POINTS
%CONSTANTS KLCL,SLACKLCL % STIFFNESS OF LCL AND SLACK LENGTH OF LCL. USED TO SPECIFY/CALCULATE FORCES GENERATED IN THE LCL
%SPECIFIED STRAINLCL,LENGTHLCL % STRAIN AND CURRENT LENGTH OF LCL
%SPECIFIED LCLFORCE % WE WILL CALCULATE THIS BASED ON FORCE EXPRESSION. NEED TO DO IT IN THE M-FILE.

%!% NEED TO ADD MCL AND PCL

% MY WORK FROM IN-CLASS ACTIVITY
% INPUT KNEE LIGAMENT PREDECLARATION INFORMATION HERE
CONSTANTS STRAINCONSTANT % 0.03 (USED TO HELP DETERMINE FORCES OF THE LIGAMENTS)

% exercise to build out LCL
POINTS TIBLCL, FEMLCL
CONSTANTS TIBREF2LCL{3}, FEMREF2LCL{3} % REFERENCE POINTS FOR THE ATTACHMENT OF THE LCL TO THE BONES
CONSTANTS LCLSLACK, KLCL % 48.8, 91.3
SPECIFIED LCLSTRAIN, LCLLENGTH % LCLSTRAIN WILL BE USED TO HELP US DETERMINE HOW FORCE IS CHANGING OVER TIME ON THE LCL
SPECIFIED LCLFORCE % WILL BE CALCULATED WITH IF STATEMENTS IN MATLAB

% build out MCL
POINTS TIBMCL, FEMMCL
CONSTANTS TIBREF2MCL{3}, FEMREF2MCL{3} % REFERENCE POINTS FOR THE MCL ATTACHMENT TO THE BONES
CONSTANTS MCLSLACK, KMCL % 121.2, 44.6
SPECIFIED MCLSTRAIN, MCLLENGTH % MCLSTRAIN WILL BE USED TO DETERMINE HOW FORCE IS CHANGING OVER TIME
SPECIFIED MCLFORCE % CALCULATED IN IF STATEMENTS IN MATLAB

% build out PCL
POINTS TIBPCL, FEMPCL
CONSTANTS TIBREF2PCL{3}, FEMREF2PCL{3} % REFERENCE POINTS FOR THE PCL ATTACHMENT TO THE BONES
CONSTANTS PCLSLACK, KPCL % 185, 27.5
SPECIFIED PCLSTRAIN, PCLLENGTH % PCLSTRAIN WILL BE USED TO DETERMINE HOW FORCE CHANGES OVER TIME
SPECIFIED PCLFORCE % CALCULATED IN IF STATEMENTS IN MATLAB
%%%%% /\ %%%%%

%%%%% \/ %%%%%
% INPUT QUAD INFORMATION HERE
VARIABLES QUADFORCE  % INTRODUCE THE QUAD FORCE THAT IS TREATED AS AN UNKNOWN.  PUT IN ZEE_NOT MATRIX

% EXAMPLE FROM DR. LACOUR OF VASTUS MEDIALIS
CONSTANTS VASTMEDRATIO % RATIO OF THE VASATUS MEDIALIS, SEE HINT 1

POINTS PATQUADVM, FEMQUADVM  % VASTUS MEDIALIS ORIGIN AND ATTACHMENT SITES
POINTS VASTMEDWRAP  % VASTUS MEDIALIS WRAPPING POINTS
CONSTANTS PATO2PATQUADVM{3}, FEMREF2FEMQUADVM{3}  % DISTANCES FROM BONE REFERENCE POINTS TO VASTUS MEDIALIS ORIGIN AND ATTACHMENT SITES
CONSTANTS FEMREF2VMWRAP{3} % DISTANCE FROM FEMUR REFERENCE TO WRAPPING POINTS ON THE FEMUR
SPECIFIED QUADVM_NOWRAP,QUADVM_WRAP % WRAPPING FACTORS TO TURN WRAPPING ON AND OFF, SEE HINT 2

%!% NEED TO DO VASTUS LATERALIS, VASTUS INTNTERMEDIUS, AND RECTUS FEMORIS

% start personal work
% VASTUS LATERALIS (VL)
CONSTANTS VASTLATRATIO % RATIO OF THE VASTUS LATERALIS

POINTS PATQUADVL, FEMQUADVL % VASTUS LATERALIS ORIGIN AND ATTACHMENT SITES
POINTS VASTLATWRAP % WRAPPING POINT FOR VASTUS LATERALIS
CONSTANTS PATO2PATQUADVL{3}, FEMREF2FEMQUADVL{3} % DISTANCES FROM BONE REF POINTS TO VL ORIGIN/ATTACHMENT SITES
CONSTANTS FEMREF2VLWRAP{3} % DISTANCE TO WRAPPING POINT FROM FEM REF
SPECIFIED QUADVL_NOWRAP, QUADVL_WRAP % WRAPPING FACTORS TO TURN WRAPPING ON AND OFF

% VASTUS INTERMEDIUS (VI) % FOLLOWS SIMILAR OUTLINE AS ABOVE!
CONSTANTS VASTINTRATIO % RATIO OF VI

POINTS PATQUADVI, FEMQUADVI % ORIGIN AND ATTACHMENT SITES
POINTS VASTINTWRAP % WRAPPING POINT FOR VI
CONSTANTS PATO2PATQUADVI{3}, FEMREF2FEMQUADVI{3} % DISTANCES FROM BONE REF POINTS TO VI ORIGIN/ATTACHMENT SITES
CONSTANTS FEMREF2VIWRAP{3} % DISTANCE FROM FEM REF TO WRAPPING POINTS ON THE FEMUR
SPECIFIED QUADVI_NOWRAP, QUADVI_WRAP % BINARY WRAPPING FACTORS

% RECTUS FEMORIS (RF) % FOLLOWS SIMILAR OUTLINE
CONSTANTS RECTFEMRATIO % RATIO OF RF

POINTS PATQUADRF, FEMQUADRF % ORIGIN AND ATTACHMENT SITES
POINTS RECTFEMWRAP % WRAPPING POINT FOR RF
CONSTANTS PATO2PATQUADRF{3}, FEMREF2FEMQUADRF{3} % DISTANCES FROM BONE REF POINTS TO RF ORIGIN/ATTACHMENT SITES
CONSTANTS FEMREF2RFWRAP{3} % DISTANCE FEM REF TO WRAPPING POINTS
SPECIFIED QUADRF_NOWRAP, QUADRF_WRAP % BINARY WRAPPING FACTORS
%%%%% /\ %%%%%

% DEFINING DIRECTION COSINE MATRIX. ASSUMING 321 BODY FIXED SEQUENCE AND THE ANGLES ARE SPECIFIED RELATIVE TO THE PREVIOUS BODY
DIRCOS(N,FOOT,BODY321,THETAFOOT3,THETAFOOT2,THETAFOOT1)

CONSTANTS TALDIR{3}
TAL>=TALDIR1*FOOT1>+TALDIR2*FOOT2>+TALDIR3*FOOT3>
SIMPROT(FOOT,TIB,TAL>,THETATIB) % ROTATION ABOUT TAL> VECTOR

DIRCOS(TIB,FEM,BODY321,THETAFEM3,THETAFEM2,THETAFEM1) % NOTE HOW THE DATA IS GIVEN IN THE PROBLEM DEFINITION
DIRCOS(TIB,PAT,BODY321,THETAPAT3,THETAPAT2,THETAPAT1) 
DIRCOS(FEM,PEL,BODY321,THETAPEL3,THETAPEL2,THETAPEL1)

% DEFINING ANGULAR VELOCITIES AND INTRODUCING GENERALIZED SPEEDS.
ANGVEL(N,FOOT) % FOR FOOT
W_FOOT_N>:=W_FOOT_N>+U1*N1>+U2*N2>+U3*N3> % OR ANY OTHER REFERENCE FRAME. YOU EQUATIONS CHANGE BASED ON YOUR DIRECTIONS.

ANGVEL(N,TIB) % FOR TIBIA
W_TIB_N>:=W_TIB_N>+U4*N1>+U5*N2>+U6*N3>

ANGVEL(N,FEM) % FOR FEMUR
W_FEM_N>:=W_FEM_N>+U7*N1>+U8*N2>+U9*N3>

ANGVEL(N,PAT) % FOR PATELLA
W_PAT_N>:=W_PAT_N>+U10*N1>+U11*N2>+U12*N3>

ANGVEL(N,PEL) % FOR PELVIS
W_PEL_N>:=W_PEL_N>+U13*N1>+U14*N2>+U15*N3>

% DEFINING POSTION VECTORS

% FOR THE FOOT
P_NO_FOOTNEWTONIAN>=0>
P_FOOTNEWTONIAN_GRF>=0>
P_FOOTNEWTONIAN_FOOTO> = FOOTNEWT2FOOTO1*FOOT1>+FOOTNEWT2FOOTO2*FOOT2>+FOOTNEWT2FOOTO3*FOOT3>
P_FOOTO_ANKLEFOOT>=FOOTO2ANKLE1*FOOT1>+FOOTO2ANKLE2*FOOT2>+FOOTO2ANKLE3*FOOT3>

P_ANKLETIB_ANKLEFOOT>=0> % ANKLE JOINT. HINGE

% FOR THE TIBIA
P_ANKLETIB_TIBREF>=ANKLE2TIBREF1*TIB1>+ANKLE2TIBREF2*TIB2>+ANKLE2TIBREF3*TIB3>
P_TIBREF_TIBO>=TIBREF2TIBO1*TIB1>+TIBREF2TIBO2*TIB2>+TIBREF2TIBO3*TIB3>
P_TIBREF_TIBPATLIG>=TIBREF2PATLIG1*TIB1>+TIBREF2PATLIG2*TIB2>+TIBREF2PATLIG3*TIB3>

%%%%% \/ %%%%%
% TIBIA LIGAMENT POSITION VECTORS
P_TIBREF_TIBLCL> = TIBREF2LCL1*TIB1> + TIBREF2LCL2*TIB2> + TIBREF2LCL3*TIB3>

%!% NEED TO ADD MCL AND PCL
P_TIBREF_TIBMCL> = TIBREF2MCL1*TIB1> + TIBREF2MCL2*TIB2> + TIBREF2MCL3*TIB3>
P_TIBREF_TIBPCL> = TIBREF2PCL1*TIB1> + TIBREF2PCL2*TIB2> + TIBREF2PCL2*TIB3>
%%%%% /\ %%%%%

% REALISTIC KNEE JOINT
P_TIBREF_FEMREF>= TIBREF2FEMREF1*TIB1>+TIBREF2FEMREF2*TIB2>+TIBREF2FEMREF3*TIB3> % FROM TIBREF TO FEMREF WHOSE POSITION VECTORS HAVE BEEN SPECIFIED. MEASURED IN THE TIBIA REFERENCE FRAME
P_FEMREF_KNEEFEMURMED>=FEMREF2KNEEFEMURMED1*FEM1>+FEMREF2KNEEFEMURMED2*FEM2>+FEMREF2KNEEFEMURMED3*FEM3> % FROM FEMREF TO KNEE CONTACT 1. POSITION VECTORS SPECIFIED. MEASURED IN THE FEMUR REFERENCE FRAME
P_FEMREF_KNEEFEMURLAT>=FEMREF2KNEEFEMURLAT1*FEM1>+FEMREF2KNEEFEMURLAT2*FEM2>+FEMREF2KNEEFEMURLAT3*FEM3> % FROM FEMREF TO KNEE CONTACT 2. POSITION VECTORS SPECIFIED. MEASURED IN THE FEMUR REFERENCE FRAME
P_KNEEFEMURMED_KNEETIBMED>=0> % KNEE CONTACT POINT MED ON TIBIA
P_KNEEFEMURLAT_KNEETIBLAT>=0> % KNEE CONTACT POINT LAT ON TIBIA

% FOR THE FEMUR
P_FEMREF_FEMO>=FEMREF2FEMO1*FEM1>+FEMREF2FEMO2*FEM2>+FEMREF2FEMO3*FEM3>
P_FEMREF_HIPFEM>=FEMREF2HIP1*FEM1>+FEMREF2HIP2*FEM2>+FEMREF2HIP3*FEM3>

%%%%% \/ %%%%%
% FEMUR LIGAMENT POSITION VECTORS
P_FEMREF_FEMLCL> = FEMREF2LCL1*FEM1> + FEMREF2LCL2*FEM2> + FEMREF2LCL3*FEM3>

%!% NEED TO ADD MCL AND PCL
P_FEMREF_FEMMCL> = FEMREF2MCL1*FEM1> + FEMREF2MCL2*FEM2> + FEMREF2MCL3*FEM3>
P_FEMREF_FEMPCL> = FEMREF2PCL1*FEM1> + FEMREF2PCL2*FEM2> + FEMREF2PCL3*FEM3>
%%%%% /\ %%%%%

%%%%% \/ %%%%%
% FEMUR QUADRICEP MUSCLE POSITION VECTORS
P_FEMREF_FEMQUADVM> = FEMREF2FEMQUADVM1*FEM1> + FEMREF2FEMQUADVM2*FEM2> + FEMREF2FEMQUADVM3*FEM3>
P_FEMREF_VASTMEDWRAP> = FEMREF2VMWRAP1*FEM1> + FEMREF2VMWRAP2*FEM2> + FEMREF2VMWRAP3*FEM3>

%!% NEED TO ADD VL, VI, AND RF
% VL
P_FEMREF_FEMQUADVL> = FEMREF2FEMQUADVL1*FEM1> + FEMREF2FEMQUADVL2*FEM2> + FEMREF2FEMQUADVL3*FEM3>
P_FEMREF_VASTLATWRAP> = FEMREF2VLWRAP1*FEM1> + FEMREF2VLWRAP2*FEM2> + FEMREF2VLWRAP3*FEM3>

% VI
P_FEMREF_FEMQUADVI> = FEMREF2FEMQUADVI1*FEM1> + FEMREF2FEMQUADVI2*FEM2> + FEMREF2FEMQUADVI3*FEM3>
P_FEMREF_VASTINTWRAP> = FEMREF2VIWRAP1*FEM1> + FEMREF2VIWRAP2*FEM2> + FEMREF2VIWRAP3*FEM3>

% RF
P_FEMREF_FEMQUADRF> = FEMREF2FEMQUADRF1*FEM1> + FEMREF2FEMQUADRF2*FEM2> + FEMREF2FEMQUADRF3*FEM3>
P_FEMREF_RECTFEMWRAP> = FEMREF2RFWRAP1*FEM1> + FEMREF2RFWRAP2*FEM2> + FEMREF2RFWRAP3*FEM3>
%%%%% /\ %%%%%

% FOR PATELLA
P_FEMREF_PATO>=FEMREF2PATO1*TIB1>+FEMREF2PATO2*TIB2>+FEMREF2PATO3*TIB3>
P_FEMREF_FEMPAT>=FEMREF2FEMPAT1*TIB1>+FEMREF2FEMPAT2*TIB2>+FEMREF2FEMPAT3*TIB3>
P_FEMPAT_PATFEM>=0> % PATELLOFEMORAL JOINT CONTACT POINT.

P_PATO_PATPATLIG>=PATO2PATLIG1*PAT1>+PATO2PATLIG2*PAT2>+PATO2PATLIG3*PAT3>

%%%%% \/ %%%%%
% PATELLA QUADRICEP MUSCLES POSITION VECTORS
P_PATO_PATQUADVM> = PATO2PATQUADVM1*PAT1> + PATO2PATQUADVM2*PAT2> + PATO2PATQUADVM3*PAT3>

%!% NEED TO ADD VL, VI, AND RF
% VL
P_PATO_PATQUADVL> = PATO2PATQUADVL1*PAT1> + PATO2PATQUADVL2*PAT2> + PATO2PATQUADVL3*PAT3>

% VI
P_PATO_PATQUADVI> = PATO2PATQUADVI1*PAT1> + PATO2PATQUADVI2*PAT2> + PATO2PATQUADVI3*PAT3>

% RF
P_PATO_PATQUADRF> = PATO2PATQUADRF1*PAT1> + PATO2PATQUADRF2*PAT2> + PATO2PATQUADRF3*PAT3>
%%%%% /\ %%%%%

% FOR THE PELVIS
P_HIPFEM_HIPPEL>=0>
P_HIPPEL_PELO>=HIPPEL2PELO1*PEL1>+HIPPEL2PELO2*PEL2>+HIPPEL2PELO3*PEL3> 
P_PELO_PELNEWTONIAN>=PELO2PELNEWT1*PEL1>+PELO2PELNEWT2*PEL2>+PELO2PELNEWT3*PEL3>

% DEFINING LINEAR VELOCITIES
V_FOOTNEWTONIAN_N>=V_NO_N>+U16*N1>+U17*N2>+U18*N3> % COINCIDENT WITH POINT 'NO'. INTRODUCING GENERALIZED SPEEDS.

% FOR THE FOOT
V2PTS(N,FOOT,FOOTNEWTONIAN,GRF)
V2PTS(N,FOOT,FOOTNEWTONIAN,FOOTO)
V2PTS(N,FOOT,FOOTNEWTONIAN,ANKLEFOOT)

V_ANKLETIB_N>=V_ANKLEFOOT_N>+U19*N1>+U20*N2>+U21*N3> % ANKLE JOINT. INTRODUCING GENERALIZED SPEEDS.

% FOR THE TIBIA
V2PTS(N,TIB,ANKLETIB,TIBREF)
V2PTS(N,TIB,TIBREF,TIBO)
V2PTS(N,TIB,TIBREF,TIBPATLIG)

%%%%% \/ %%%%%
% TIBIAL LIGAMENT LINEAR VELOCITIES
V2PTS(N,TIB,TIBREF,TIBLCL)

%!% NEED TO ADD MCL AND PCL
V2PTS(N,TIB,TIBREF,TIBMCL)
V2PTS(N,TIB,TIBREF,TIBPCL)
%%%%% /\ %%%%%

% REALSTIC MODELING OF THE KNEE
V_FEMREF_N>=V_TIBREF_N> +DT(P_TIBREF_FEMREF>,N)+U22*TIB1>+U23*TIB2>+U24*TIB3> % IN THIS CASE U22,23,24 SHOULD BE ZERO, CHECK THE CONSTRAIN COMMAND

V2PTS(N,FEM,FEMREF,KNEEFEMURMED)  % VELOCITY OF KNEE CONTACT 1 ON THE FEMUR. NOTE THE USE OF THE V2PTS COMMAND INSTEAD OF THE DT COMMAND. THIS ENSURES THAT THERE IS RELATIVE VELOCITY BETWEEN THE TWO COINCIDENT CONTACT POINTS.
V2PTS(N,FEM,FEMREF,KNEEFEMURLAT)  % VELOCITY OF KNEE CONTACT 2 ON THE FEMUR. 
V2PTS(N,TIB,TIBREF,KNEETIBMED) % VELOCITY OF KNEE CONTACT 1 ON THE TIBIA.
V2PTS(N,TIB,TIBREF,KNEETIBLAT) % VELOCITY OF KNEE CONTACT 2 ON THE TIBIA

% FOR THE FEMUR
V2PTS(N,FEM,FEMREF,FEMO)
V2PTS(N,FEM,FEMREF,HIPFEM)

%%%%% \/ %%%%%
% FEMUR LIGAMENT LINEAR VELOCITIES
V2PTS(N,FEM,FEMREF,FEMLCL)

%!% NEED TO ADD MCL AND PCL
V2PTS(N,FEM,FEMREF,FEMMCL)
V2PTS(N,FEM,FEMREF,FEMPCL)
%%%%% /\ %%%%%

%%%%% \/ %%%%%
% FEMUR QUADRICEP LINEAR VELOCITIES
V2PTS(N,FEM,FEMREF,FEMQUADVM)
V2PTS(N,FEM,FEMREF,VASTMEDWRAP)

%!% NEED TO ADD VL, VI, AND RF
% VL
V2PTS(N,FEM,FEMREF,FEMQUADVL)
V2PTS(N,FEM,FEMREF,VASTLATWRAP)

% VI
V2PTS(N,FEM,FEMREF,FEMQUADVI)
V2PTS(N,FEM,FEMREF,VASTINTWRAP)

% RF
V2PTS(N,FEM,FEMREF,FEMQUADRF)
V2PTS(N,FEM,FEMREF,RECTFEMWRAP)
%%%%% /\ %%%%%

% FOR THE PATELLA
V_PATO_N>=V_FEMREF_N>+DT(P_FEMREF_PATO>,N)+U25*TIB1>+U26*TIB2>+U27*TIB3> % PATELLOFEMORAL JOINT. REALISTIC MODELING SIMILAR TO THE KNEE. U22, U23, U24 ARE ZEROS
V2PTS(N,FEM,FEMREF,FEMPAT) % VELOCITY OF PATELLOFEMORAL CONTACT POINT ON FEMUR
V2PTS(N,PAT,PATO,PATFEM) % NOTE THE USE OF THE V2PTS COMMAND TO MODEL RELATIVE VELOCITY
V2PTS(N,PAT,PATO,PATPATLIG)

%%%%% \/ %%%%%
% PATELLA QUADRICEP LINEAR VELOCITIES
V2PTS(N,PAT,PATO,PATQUADVM)

%!% NEED TO ADD VL, VI, AND RF
% VL
V2PTS(N,PAT,PATO,PATQUADVL)

% VI
V2PTS(N,PAT,PATO,PATQUADVI)

% RF
V2PTS(N,PAT,PATO,PATQUADRF)
%%%%% /\ %%%%%

% FOR THE PELVIS
V_HIPPEL_N>=V_HIPFEM_N>+U28*N1>+U29*N2>+U30*N3> % HIP JOINT
V2PTS(N,PEL,HIPPEL,PELO) % FOR POINT ON THE PELVIS
V2PTS(N,PEL,PELO,PELNEWTONIAN)

% CONSTRAINTS AND KANE COMMAND
AUXILIARY[1]=U1-0
AUXILIARY[2]=U2-0
AUXILIARY[3]=U3-0
AUXILIARY[4]=U4-0
AUXILIARY[5]=U5-0
AUXILIARY[6]=U6-0
AUXILIARY[7]=U7-0
AUXILIARY[8]=U8-0
AUXILIARY[9]=U9-0
AUXILIARY[10]=U10
AUXILIARY[11]=U11
AUXILIARY[12]=U12
AUXILIARY[13]=U13
AUXILIARY[14]=U14
AUXILIARY[15]=U15
AUXILIARY[16]=U16-0 % BALL AND SOCKET
AUXILIARY[17]=U17-0 % BALL AND SOCKET
AUXILIARY[18]=U18-0 % BALL AND SOCKET
AUXILIARY[19]=U19-0 % BALL AND SOCKET
AUXILIARY[20]=U20-0 % BALL AND SOCKET
AUXILIARY[21]=U21-0 % BALL AND SOCKET
AUXILIARY[22]=U22-0 % VELOCITY WAS SPECIFIED TO DEFINE KNEE JOINT.
AUXILIARY[23]=U23-0 % VELOCITY WAS SPECIFIED TO DEFINE KNEE JOINT.
AUXILIARY[24]=U24-0 % VELOCITY WAS SPECIFIED TO DEFINE KNEE JOINT.
AUXILIARY[25]=U25-0 
AUXILIARY[26]=U26-0 
AUXILIARY[27]=U27-0 
AUXILIARY[28]=U28-0 
AUXILIARY[29]=U29-0
AUXILIARY[30]=U30-0 

CONSTRAIN(AUXILIARY[U1,U2,U3,U4,U5,U6,U7,U8,U9,U10,U11,U12,U13,U14,U15,U16,U17,U18,U19,U20,U21,U22,U23,U24,U25,U26,U27,U28,U29,U30]) % CONSTRAINING ALL

% GOOD TO PUT ALL MAJOR VARIABLES IN ZEE_NOT MATRIX.
ZEE_NOT=[ANKLEFORCE1,ANKLEFORCE2,ANKLEFORCE3,FOOTTOR1,FOOTTOR2,FOOTTOR3]
ZEE_NOT:=[ZEE_NOT,KNEEFORCEMED1,KNEEFORCEMED2,KNEEFORCEMED3,KNEEFORCELAT1,KNEEFORCELAT2,KNEEFORCELAT3]
ZEE_NOT:=[ZEE_NOT,HIPFORCE1,HIPFORCE2,HIPFORCE3,PATFEMFORCE1,PATFEMFORCE2,PATFEMFORCE3,SPINEFORCE1,SPINEFORCE2,SPINEFORCE3]
ZEE_NOT:=[ZEE_NOT,FEMTOR1,FEMTOR2,FEMTOR3,TIBTOR1,TIBTOR2,TIBTOR3,PATTOR1,PATTOR2,PATTOR3,PELTOR1,PELTOR2,PELTOR3]
ZEE_NOT:=[ZEE_NOT,PATLIGFORCE,QUADFORCE]

%%%%% \/ %%%%%
% INPUT SPECIFIED INFORMATION AND CONSTANT INPUTS HERE
% MAKES IT EASIER ON THE GRADER TO CHECK ALL YOUR DEFINITIONS
% SINCE EVERYTHING IS IN THE SAME LOCATION.
% WARNING: MAKE SURE SURE YOU SPECIFY ALL THE INPUTS BEFORE USING THE "EVALUATE" COMMAND.

% RANDOM INPUT STUFF
BW = 867.204
TALDIR1=-0.105014 
TALDIR2=-0.174022 
TALDIR3=0.979126
INPUT STRAINCONSTANT = 0.03

%!% ABOVE AND BELOW ARE A FEW LOSS-INTUITIVE EXAMPLES.  YOU NEED TO ADD EVERYTHING ELSE!!
%!% DO EVERYTHING FROM ALL OF THE TABLES, DON'T FORGET ABOUT THE STUFF ON PAGE 12
%!% DON'T FORGET ABOUT THE QUADRICEP MUSCLE RATIOS (HINT 1)

% QUADRICEP MUSCLE RATIOS
INPUT VASTMEDRATIO = 44.4/225.8
INPUT VASTLATRATIO = 75.1/225.8
INPUT VASTINTRATIO = 56.5/225.8
INPUT RECTFEMRATIO = 49.8/225.8

% APPLYING INERTIA TERMS
MASSFOOT=0.0128*(BW/G)
INPUT IFOOT1=0.00006, IFOOT2=0.00028, IFOOT3=0.00024

% TIBIA
MASSTIB = 0.047*(BW/G)
INPUT ITIB1 = 0.0641, ITIB2 = 0.0046, ITIB3 = 0.0641

% FEMUR
MASSFEM = 0.11*(BW/G)
INPUT IFEM1 = 0.1338, IFEM2 = 0.0296, IFEM3 = 0.1338

% PELVIS
MASSPEL = 0.12*(BW/G)
INPUT IPEL1 = 0.048, IPEL2 = 0.050, IPEL3 = 0.041

% PATELLA (NEGLIGIBLE MASS AND MOI)
MASSPAT = 0*(BW/G)
INPUT IPAT1 = 0, IPAT2 = 0, IPAT3 = 0

% CONSTANT INPUT VALUES
% FOOT
INPUT FOOTNEWT2FOOTO1=-0.065,FOOTNEWT2FOOTO2=0.005,FOOTNEWT2FOOTO3=0
INPUT FOOTO2ANKLE1 = -0.065, FOOTO2ANKLE2 = 0.020, FOOTO2ANKLE3 = 0

% TIBIA
INPUT ANKLE2TIBREF1 = -0.01, ANKLE2TIBREF2 = -0.36, ANKLE2TIBREF3 = 0
INPUT TIBREF2TIBO1 = -0.0032, TIBREF2TIBO2 = -0.163, TIBREF2TIBO3 = 0.0051
INPUT TIBREF2PATLIG1 = 0.025, TIBREF2PATLIG2 = -0.015, TIBREF2PATLIG3 = -0.016
INPUT TIBREF2MCL1 = 0.0014, TIBREF2MCL2 = 0.0104, TIBREF2MCL3 = 0.0449
INPUT TIBREF2LCL1 = -0.0146, TIBREF2LCL2 = 0.0084, TIBREF2LCL3 = -0.0331
INPUT TIBREF2PCL1 = -0.0186, TIBREF2PCL2 = 0.0174, TIBREF2PCL3 = 0.0129

% PATELLA
INPUT PATO2PATLIG1 = 0.0110, PATO2PATLIG2 = -0.0210, PATO2PATLIG3 = -0.0025
INPUT PATO2PATQUADRF1 = 0.0130, PATO2PATQUADRF2 = 0.0195, PATO2PATQUADRF3 = 0.0080
INPUT PATO2PATQUADVL1 = 0.01, PATO2PATQUADVL2 = 0.0195, PATO2PATQUADVL3 = -0.005
INPUT PATO2PATQUADVM1 = 0.0130, PATO2PATQUADVM2 = 0.0160, PATO2PATQUADVM3 = 0.0130
INPUT PATO2PATQUADVI1 = 0.012, PATO2PATQUADVI2 = 0.0215, PATO2PATQUADVI3 = 0.0020

% FEMUR
INPUT FEMREF2FEMO1 = -0.0080, FEMREF2FEMO2 = 0.2743, FEMREF2FEMO3 = -0.0120
INPUT FEMREF2HIP1 = -0.0070, FEMREF2HIP2 = 0.47, FEMREF2HIP3 = 0.002
INPUT FEMREF2FEMQUADVL1 = 0.0575, FEMREF2FEMQUADVL2 = 0.4306, FEMREF2FEMQUADVL3 = -0.0420
INPUT FEMREF2FEMQUADVM1 = 0.055, FEMREF2FEMQUADVM2 = 0.4196, FEMREF2FEMQUADVM3 = -0.0250
INPUT FEMREF2FEMQUADVI1 = 0.056, FEMREF2FEMQUADVI2 = 0.4106, FEMREF2FEMQUADVI3 = -0.0350
INPUT FEMREF2FEMQUADRF1 = 0.074, FEMREF2FEMQUADRF2 = 0.4276, FEMREF2FEMQUADRF3 = -0.0265
INPUT FEMREF2MCL1 = 0.0040, FEMREF2MCL2 = 0.0012, FEMREF2MCL3 = 0.0401
INPUT FEMREF2LCL1 = 0.0050, FEMREF2LCL2 = 0.0018, FEMREF2LCL3 = -0.0405
INPUT FEMREF2PCL1 = -0.0122, FEMREF2PCL2 = -0.0030, FEMREF2PCL3 = 0.0105
INPUT FEMREF2VLWRAP1 = 0.0386, FEMREF2VLWRAP2 = -0.0112, FEMREF2VLWRAP3 = -0.0094
INPUT FEMREF2VMWRAP1 = 0.0362, FEMREF2VMWRAP2 = -0.0103, FEMREF2VMWRAP3 = 0.0081
INPUT FEMREF2VIWRAP1 = 0.0371, FEMREF2VIWRAP2 = -0.0103, FEMREF2VIWRAP3 = -0.0027
INPUT FEMREF2RFWRAP1 = 0.0347, FEMREF2RFWRAP2 = -0.0100, FEMREF2RFWRAP3 = 0.0032

% PELVIS
INPUT HIPPEL2PELO1 = 0.0134, HIPPEL2PELO2 = -0.0274, HIPPEL2PELO3 = -0.0200
INPUT PELO2PELNEWT1 = -0.0260, PELO2PELNEWT2 = 0.0960, PELO2PELNEWT3 = 0.0460

% TIME-DEPENDENT SPECIFIED VALUES
% TABLE 1 (PG 8)
THETATIB = (-0.0727*T^3 + 1.7314*T^2 - 14.075*T + 8.9013)*CONDTOR
THETAFEM1 = (-0.005*T^3 + 0.2189*T^2 - 1.7709*T + 0.8077)*CONDTOR
THETAFEM2 = (0.0077*T^3 - 0.0733*T^2 - 0.2519*T + 2.942)*CONDTOR
THETAFEM3 = (0.0581*T^3 - 1.9923*T^2 + 25.827*T + 3.8154)*CONDTOR

% TABLE 2 (PG 8)
THETAPEL1 = (0.005*T^3 - 0.2189*T^2 + 1.7709*T - 0.8077)*CONDTOR
THETAPEL2 = (-0.0077*T^3 + 0.0733*T^2 + 0.2519*T - 2.942)*CONDTOR
THETAPEL3 = (0.0356*T^3 - 0.0893*T^2 - 12.519*T - 0.0335)*CONDTOR
THETAPAT1 = 0*CONDTOR
THETAPAT2 = (0.0393*T^3 - 0.7464*T^2 + 4.8655*T - 2.4242)*CONDTOR
THETAPAT3 = (0.0203*T^3 - 0.4687*T^2 + 4.355*T + 0.4456)*CONDTOR

% TABLE 3 (PG 8)
% GRFFORCE1 = 0.0842*T^3 - 0.638*T^2 + 0.9228*T - 39.671
GRFFORCE1 = 0.0848*T^3 - 0.6473*T^2 + 0.9585*T - 39.703
GRFFORCE2 = -0.2008*T^3 - 1.0892*T^2 + 29.544*T + 226.91
GRFFORCE3 = -0.166*T^3 + 1.9321*T^2 - 7.9625*T + 38.526

% NEED TO CONVERT FOLLOWING 5 TABLES TO M FROM MM (/1000)
% TABLE 1 (PG 9)
TIBREF2FEMREF1 = (0.063*T^3 - 0.9946*T^2 + 2.6602*T - 2.6224)/1000
TIBREF2FEMREF2 = (-0.007*T^3 - 0.1063*T^2 + 1.3466*T + 50.348)/1000
TIBREF2FEMREF3 = (6)/1000

% TABLE 2 (PG 9)
FEMREF2KNEEFEMURMED1 = (-0.0707*T^3 + 1.7786*T^2 - 14.133*T + 7.3319)/1000
FEMREF2KNEEFEMURMED2 = (-0.0648*T^3 + 0.848*T^2 + 0.163*T - 26.124)/1000
FEMREF2KNEEFEMURMED3 = (25)/1000

% TABLE 3 (PG 9)
FEMREF2KNEEFEMURLAT1 = (-0.0331*T^3 + 1.0046*T^2 - 8.6988*T - 2.7256)/1000
FEMREF2KNEEFEMURLAT2 = (-0.0423*T^3 + 0.7067*T^2 + 0.3543*T - 26.859)/1000
FEMREF2KNEEFEMURLAT3 = (-28)/1000

% TABLE 1 (PG 10)
FEMREF2PATO1 = (0.0174*T^3 - 0.204*T^2 + 0.3034*T + 44.014)/1000
FEMREF2PATO2 = (0.0004*T^3 + 0.1369*T^2 - 1.5433*T + 9.1067)/1000
FEMREF2PATO3 = (-6)/1000

% TABLE 2 (PG 10)
FEMREF2FEMPAT1 = (-0.0141*T^3 + 0.4015*T^2 - 3.366*T + 24.033)/1000
FEMREF2FEMPAT2 = (-0.0067*T^3 + 0.0776*T^2 - 0.3468*T + 3.0077)/1000
FEMREF2FEMPAT3 = (0)/1000


%%%%% /\ %%%%%

% APPLYING KNOWN GRF FORCES
FORCE_GRF>=GRFFORCE1*N1>+GRFFORCE2*N2>+GRFFORCE3*N3> % GROUND REACTION FORCE

% APPLYING JOINT FORCES AND TORQUES
FORCE(ANKLEFOOT/ANKLETIB,ANKLEFORCE1*FOOT1>+ANKLEFORCE2*FOOT2>+ANKLEFORCE3*FOOT3>) % ANKLE CONTACT FORCE
FORCE(KNEETIBMED/KNEEFEMURMED,KNEEFORCEMED1*TIB1>+KNEEFORCEMED2*TIB2>+KNEEFORCEMED3*TIB3>) % KNEE CONTACT 1 FORCE
FORCE(KNEETIBLAT/KNEEFEMURLAT,KNEEFORCELAT1*TIB1>+KNEEFORCELAT2*TIB2>+KNEEFORCELAT3*TIB3>) % KNEE CONTACT 2 FORCE
FORCE(FEMPAT/PATFEM,PATFEMFORCE1*PAT1>+PATFEMFORCE2*PAT2>+PATFEMFORCE3*PAT3>) % PATELLOFEMORAL FORCE
FORCE(HIPFEM/HIPPEL,HIPFORCE1*FEM1>+HIPFORCE2*FEM2>+HIPFORCE3*FEM3>) % HIP FORCE
FORCE_PELNEWTONIAN>=SPINEFORCE1*N1>+SPINEFORCE2*N2>+SPINEFORCE3*N3>	% SPINE FORCE AT THE TOP OF THE PELVIS

TORQUE(FOOT,FOOTTOR1*FOOT1>+FOOTTOR2*FOOT2>+FOOTTOR3*FOOT3>) % ON THE FOOT
TORQUE(TIB,TIBTOR1*TIB1>+TIBTOR2*TIB2>+TIBTOR3*TIB3>) % ON THE TIBIA
TORQUE(FEM,FEMTOR1*FEM1>+FEMTOR2*FEM2>+FEMTOR3*FEM3>) % ON THE FEMUR
TORQUE(PAT,PATTOR1*PAT1>+PATTOR2*PAT2>+PATTOR3*PAT3>) % ON THE PATELLA
TORQUE(PEL,PELTOR1*PEL1>+PELTOR2*PEL2>+PELTOR3*PEL3>) % ON THE PELVIS

%%%%% \/ %%%%%
% LIGAMENT FORCES

%SLACKLCL=48.8/1000 % SLACK LENGTHS
%INPUT KLCL = 91.3*1000
%STRAINLCL=(MAG(P_FEMLCL_TIBLCL>)-SLACKLCL)/SLACKLCL % DEFINED AS ELONGATION/ORIGINAL LENGTH
%LENGTHLCL=MAG(P_FEMLCL_TIBLCL>)
%LCLFORCE=KLCL*STRAINLCL*REFSTRAIN*LENGTHLCL % THIS IS JUST A DUMMY AND NEEDS TO BE UPDATED IN THE M-FILE!!

% APPLYING SOFT TISSUE FORCES
% FORCE(TIBLCL/FEMLCL,LCLFORCE*UNITVEC(P_FEMLCL_TIBLCL>)) % THIS IS SPECIFIED. DO NOT FORGET TO INPUT THE CORRECT DIRECTION.

%!% NEED TO ADD MCL AND PCL
% dummy formulas to make sure ligaments appear in matlab

% LCL
INPUT LCLSLACK = 48.8/1000 % CONVERT MM TO METERS
INPUT KLCL = 91.3*1000 % CONVERT N/MM TO N/M
LCLSTRAIN = (MAG(P_FEMLCL_TIBLCL>)-LCLSLACK)/LCLSLACK
LCLLENGTH = MAG(P_FEMLCL_TIBLCL>)
LCLFORCE = KLCL*LCLSLACK*LCLSTRAIN*LCLLENGTH*STRAINCONSTANT

FORCE(TIBLCL/FEMLCL,UNITVEC(P_FEMLCL_TIBLCL>)*LCLFORCE)

% MCL
INPUT MCLSLACK = 44.6/1000 % CONVERT FROM MM TO METERS
INPUT KMCL = 121.2*1000 % CONVERT N/MM TO N/M
MCLSTRAIN = (MAG(P_FEMMCL_TIBMCL>)-MCLSLACK)/MCLSLACK
MCLLENGTH = MAG(P_FEMMCL_TIBMCL>)
MCLFORCE = KMCL*MCLSLACK*MCLSTRAIN*MCLLENGTH*STRAINCONSTANT

FORCE(TIBMCL/FEMMCL,UNITVEC(P_FEMMCL_TIBMCL>)*MCLFORCE)

% PCL
INPUT PCLSLACK = 27.5/1000 % CONVERSION FROM MM TO M
INPUT KPCL = 185*1000 % CONVERT FROM N/MM TO N/M
PCLSTRAIN = (MAG(P_FEMPCL_TIBPCL>)-PCLSLACK)/PCLSLACK
PCLLENGTH = MAG(P_FEMPCL_TIBPCL>)
PCLFORCE = KPCL*PCLSLACK*PCLSTRAIN*PCLLENGTH*STRAINCONSTANT

FORCE(TIBPCL/FEMPCL,UNITVEC(P_FEMPCL_TIBPCL>)*PCLFORCE)
%%%%% /\ %%%%%

%%%%% \/ %%%%%
% APPLY QUAD FORCES AND MUSCLE WRAPPING CHECKS.
%FORCE_FEMQUADVM> = QUADVM_NOWRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVM_PATQUADVM>)
%FORCE_VASTMEDWRAP> = QUADVM_WRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVM_PATQUADVM>)

FORCE(PATQUADVM/FEMQUADVM,QUADVM_NOWRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVM_PATQUADVM>))
FORCE(PATQUADVM/VASTMEDWRAP,QUADVM_WRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_VASTMEDWRAP_PATQUADVM>))   % WRAPPING AROUND THE INFERIOR WRAPPING POINT
%!% NEED TO ADD VL, VI, AND RF
% VL
%FORCE_FEMQUADVL> = QUADVL_NOWRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVL_PATQUADVL>)
%FORCE_VASTMEDWRAP> = QUADVL_WRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVL_PATQUADVL>)

FORCE(PATQUADVL/FEMQUADVL, QUADVL_NOWRAP*VASTLATRATIO*QUADFORCE*UNITVEC(P_FEMQUADVL_PATQUADVL>))
FORCE(PATQUADVM/VASTLATWRAP, QUADVL_WRAP*VASTLATRATIO*QUADFORCE*UNITVEC(P_VASTLATWRAP_PATQUADVL>))

% VI
%FORCE_FEMQUADVI> = QUADVI_NOWRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVI_PATQUADVI>)
%FORCE_VASTMEDWRAP> = QUADVI_WRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADVI_PATQUADVI>)

FORCE(PATQUADVI/FEMQUADVI, QUADVI_NOWRAP*VASTINTRATIO*QUADFORCE*UNITVEC(P_FEMQUADVI_PATQUADVI>))
FORCE(PATQUADVI/VASTINTWRAP, QUADVI_WRAP*VASTINTRATIO*QUADFORCE*UNITVEC(P_VASTINTWRAP_PATQUADVI>))

% RF
%FORCE_FEMQUADRF> = QUADRF_NOWRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADRF_PATQUADRF>)
%FORCE_VASTMEDWRAP> = QUADRF_WRAP*VASTMEDRATIO*QUADFORCE*UNITVEC(P_FEMQUADRF_PATQUADRF>)

FORCE(PATQUADRF/FEMQUADRF, QUADRF_NOWRAP*RECTFEMRATIO*QUADFORCE*UNITVEC(P_FEMQUADRF_PATQUADRF>))
FORCE(PATQUADRF/RECTFEMWRAP, QUADRF_WRAP*RECTFEMRATIO*QUADFORCE*UNITVEC(P_RECTFEMWRAP_PATQUADRF>))

% PATELLAR LIGAMENT FORCE
FORCE(TIBPATLIG/PATPATLIG,PATLIGFORCE*UNITVEC(P_PATPATLIG_TIBPATLIG>)) % THIS IS BEING CALCULATED. DO NOT FORGET TO INPUT THE CORRECT DIRECTION.

QUADVM_NOWRAP = 1
QUADVM_WRAP = 0
%!% NEED TO ADD VL, VI, AND RF
% VL
QUADVL_NOWARP = 1
QUADVL_WRAP = 0

% VI
QUADVI_NOWRAP = 1
QUADVI_WRAP = 0

% RF
QUADRF_NOWRAP = 1
QUADRF_WRAP = 0

%%%%% /\ %%%%%

% REDUCING THE NUMBER OF UNKNOWNS BY SAYING THE MEDIAL AND THE LATERAL KNEE FORCES ARE EQUAL.
KNEEFORCELAT1=KNEEFORCEMED1
KNEEFORCELAT2=KNEEFORCEMED2
KNEEFORCELAT3=KNEEFORCEMED3

ZERO=FR()+FRSTAR() % CREATING EQUATIONS OF MOTION
SAVE CHECK.ALL  % PUT IN A SAVE CHECK.ALL COMMAND TO CHECK THE EQUATION SO THAT YOU CAN DECIDE ON THE MULTIPLE SOLVE COMMANDS TO USE

%%%%% \/ %%%%%
%KANE(MINORS,ANKLEFORCE1,ANKLEFORCE2,ANKLEFORCE3,FOOTTOR1,FOOTTOR2,FOOTTOR3,KNEEFORCEMED1,KNEEFORCEMED2,KNEEFORCEMED3, % THE & AT THE END IS FOR CONTINUATION TO THE NEXT LINE
%HIPFORCE1,HIPFORCE2,HIPFORCE3,PATFEMFORCE1,PATFEMFORCE2,PATFEMFORCE3,SPINEFORCE1,SPINEFORCE2,SPINEFORCE3,
%FEMTOR1,FEMTOR2,FEMTOR3,TIBTOR1,TIBTOR2,TIBTOR3,PATTOR1,PATTOR2,PATTOR3,PELTOR1,PELTOR2,PELTOR3) % SOLVING FOR 30 UNKNOWNS

%!% I WENT AHEAD AND DID MOST OF THIS FOR YOU
%!% YOU WILL NEED TO REMOVE 2 TORQUES AND REPLACE THEM WITH QUADFORCE (FEMTOR3) AND PATLIG (TIBTOR3) FORCE
%!% YOU NEED TO DECIDE WHICH TORQUE TO REMOVE!!!
%!% HOPEFULLY THE REST OF THIS DOESN'T CHANGE IN YOUR CODE, BUT IT MAY :-(

% NEED TO USE MINORS AND MUTLIPLE SOLVE COMMANDS FOR A SMOOTH NUMERICAL SOLUTION.
MAT1=[ZERO[1];ZERO[2];ZERO[3]]
EXPLICIT(MAT1,FOOTTOR1,FOOTTOR2, FOOTTOR3)
SOLVE(MINORS,MAT1,FOOTTOR1,FOOTTOR2, FOOTTOR3)

MAT2=[ZERO[4];ZERO[5];ZERO[6]]
EXPLICIT(MAT2,TIBTOR1,TIBTOR2,TIBTOR3)
SOLVE(MINORS,MAT2,TIBTOR1,TIBTOR2,TIBTOR3)

MAT3=[ZERO[7];ZERO[8];ZERO[9]]
EXPLICIT(MAT3,FEMTOR1,FEMTOR2,QUADFORCE)
SOLVE(MINORS,MAT3,FEMTOR1,FEMTOR2,QUADFORCE)

MAT4=[ZERO[10];ZERO[11];ZERO[12]]
EXPLICIT(MAT4,PATTOR1,PATTOR2,PATLIGFORCE)
SOLVE(MINORS,MAT4,PATTOR1,PATTOR2,PATLIGFORCE)

MAT5=[ZERO[13];ZERO[14];ZERO[15]]
EXPLICIT(MAT5,PELTOR1,PELTOR2,PELTOR3)
SOLVE(MINORS,MAT5,PELTOR1,PELTOR2,PELTOR3)

MAT6=[ZERO[16];ZERO[17];ZERO[18]]
EXPLICIT(MAT6,SPINEFORCE1,SPINEFORCE2,SPINEFORCE3)
SOLVE(MINORS,MAT6,SPINEFORCE1,SPINEFORCE2,SPINEFORCE3)

MAT7=[ZERO[19];ZERO[20];ZERO[21]]
EXPLICIT(MAT7,ANKLEFORCE1,ANKLEFORCE2,ANKLEFORCE3)
SOLVE(MINORS,MAT7,ANKLEFORCE1,ANKLEFORCE2,ANKLEFORCE3)

MAT8=[ZERO[22];ZERO[23];ZERO[24]]
EXPLICIT(MAT8,KNEEFORCEMED1,KNEEFORCEMED2,KNEEFORCEMED3)
SOLVE(MINORS,MAT8,KNEEFORCEMED1,KNEEFORCEMED2,KNEEFORCEMED3)

MAT9=[ZERO[25];ZERO[26];ZERO[27]]
EXPLICIT(MAT9,PATFEMFORCE1,PATFEMFORCE2,PATFEMFORCE3)
SOLVE(MINORS,MAT9,PATFEMFORCE1,PATFEMFORCE2,PATFEMFORCE3)

MAT10=[ZERO[28];ZERO[29];ZERO[30]]
EXPLICIT(MAT10,HIPFORCE1,HIPFORCE2,HIPFORCE3)
SOLVE(MINORS,MAT10,HIPFORCE1,HIPFORCE2,HIPFORCE3)
%%%%% /\ %%%%%

%%%%% \/ %%%%%
% UPDATE YOUR OUTPUT COMMANDS HERE

%!% I HAVE INCLUDED SOME EXAMPLES, BUT NOT EVERYTHING!!

RESGRFFORCE = (GRFFORCE1^2+GRFFORCE2^2+GRFFORCE3^2)^0.5
VASTMEDFORCE = QUADFORCE*VASTMEDRATIO
VASTLATFORCE = QUADFORCE*VASTLATRATIO
VASTINTFORCE = QUADFORCE*VASTINTRATIO
RECTFEMFORCE = QUADFORCE*RECTFEMRATIO
OUTPUT T,THETAFOOT1,THETAFEM1,THETAPAT1,THETAPEL1
OUTPUT T,KNEEFORCEMED1,KNEEFORCEMED2,KNEEFORCEMED3
OUTPUT T,LCLFORCE,LCLSTRAIN,LCLLENGTH
OUTPUT T,MCLFORCE,MCLSTRAIN,MCLLENGTH
OUTPUT T,PCLFORCE,PCLSTRAIN,PCLLENGTH
% KINETICS PLOTS
OUTPUT T,QUADFORCE,PATLIGFORCE % 1
OUTPUT T,KNEEFORCEMED1,KNEEFORCEMED2,KNEEFORCEMED3,PATFEMFORCE1,PATFEMFORCE2,PATFEMFORCE3 % 2
OUTPUT T,VASTMEDFORCE,VASTLATFORCE,VASTINTFORCE,RECTFEMFORCE % 3
OUTPUT T,LCLFORCE,MCLFORCE,PCLFORCE % 4
OUTPUT T,FOOTTOR1,TIBTOR1,FEMTOR1,PELTOR1,PATTOR1 % 5
OUTPUT T,FOOTTOR2,TIBTOR2,FEMTOR2,PELTOR2,PATTOR2 % 6
OUTPUT T,FOOTTOR3,TIBTOR3,FEMTOR3,PELTOR3,PATTOR3 % 7
% KINEMATICS PLOTS
OUTPUT T,GRFFORCE1,GRFFORCE2,GRFFORCE3 % 1
OUTPUT T,THETATIB,THETAFEM3,THETAPAT3 % 2
OUTPUT T,TIBREF2FEMREF1,TIBREF2FEMREF2,TIBREF2FEMREF3 % 3
OUTPUT T,FEMREF2PATO1,FEMREF2PATO2,FEMREF2PATO3 % 4?
OUTPUT T,FEMREF2FEMPAT1,FEMREF2FEMPAT2,FEMREF2FEMPAT3 % 4?

%%%%% /\ %%%%%

ANIMATE(N,NO,FEM,tib,pat,foot,pel)

CODE ALGEBRAIC() [T=0,9.2,0.1] KNEEMODEL_INVERSE.M % GENERATING A MATLAB M-FILE
